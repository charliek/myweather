buildscript {
    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/gesellix/gradle-plugins" }
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:0.9.7"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
        classpath 'de.gesellix:gradle-debian-plugin:14'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply from: file('gradle/versioning.gradle')
apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'pkg-debian'
apply plugin: 'docker'

repositories {
    jcenter()
    maven { url "http://repo.springsource.org/repo" } // for springloaded
}

dependencies {
    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    springloaded "org.springsource.loaded:springloaded:1.1.5.RELEASE"

    // Default SLF4J binding.  Note that this is a blocking implementation.
    // See here for a non blocking appender http://logging.apache.org/log4j/2.x/manual/async.html
    runtime 'org.slf4j:slf4j-simple:1.7.7'

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"

    compile ratpack.dependency("jackson")
    compile ratpack.dependency("rx")
    compile ratpack.dependency("hystrix")
}

// Stage is the task called by heroku
task stage {
    dependsOn installShadowApp
}

// Setup the debian package build
debian {
    packagename = project.name
    controlDirectory = "${projectDir}/src/debian/control"
    changelogFile = "${projectDir}/src/debian/changelog"
    outputFile = "${buildDir}/${project.name}_${version}_all.deb"
    data {
        dir {
            name = "${projectDir}/src/debian/data"
        }
        file {
            name = project.shadowJar.outputs.files.singleFile
            target = "usr/share/${project.name}/app.jar"
        }
    }
}

docker {
    apiUsername dockerUser
    apiPassword dockerPassword
    apiEmail dockerEmail
}

task buildDocker(type: Docker, dependsOn: shadowJar) {
    applicationName = "${dockerUser}/${project.name}"
    tagVersion = project.version
    dockerfile = project.file('Dockerfile')
    addFile(project.shadowJar.outputs.files.singleFile, "/app.jar")
    push = project.hasProperty('pushImage') && pushImage && dockerPassword
}

String getDockerPassword() {
    if (project.hasProperty('dockerPassword')) {
        return project.property('dockerPassword')
    }
    return System.getenv('DOCKER_PASSWORD')
}